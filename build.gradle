buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.3.0'
        classpath 'net.researchgate:gradle-release:2.7.0'
    }
}

plugins {
    id 'org.gradle.java-gradle-plugin'
    id 'org.gradle.groovy'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.7.0'
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'idea'

group='net.researchgate'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testCompile("org.spockframework:spock-core:$spockVersion") { exclude module: 'groovy-all' }
    testCompile "junit:junit:$junitVersion"
    testCompile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
    testCompile "cglib:cglib-nodep:$cglibVersion"
    testCompile gradleTestKit()
}

task prepareTestJar(type: Copy) {
    dependsOn jar
    from "${jar.archivePath}"
    into "${buildDir}/tmp/testJars/"
    rename '.*', 'gradle-release-0.0.0.jar'
}

test {
    dependsOn prepareTestJar
    maxParallelForks = 2
}

gradlePlugin {
    plugins {
        releasePlugin {
            id = 'net.researchgate.release'
            implementationClass = 'net.researchgate.release.ReleasePlugin'
        }
    }
}

release {
    git {
        requireBranch = '(master|\\d+\\.\\d+|\\d+\\.+\\D)'
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : ''
    key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
    publications = ['gradleReleasePublication']
    pkg {
        name = project.name
        repo = 'gradle-plugins'
        userOrg = 'researchgate'
        licenses = ['MIT']
        version {
            name = "${project.version}"
            vcsTag = "${project.version}"
            gpg {
                sign = false
            }
            mavenCentralSync {
                sync = false
                user = project.hasProperty('sonatypeUser') ? sonatypeUser : ''
                password = project.hasProperty('sonatypePassword') ? sonatypePassword : ''
            }
        }
    }
    publish = false
}

afterReleaseBuild.dependsOn bintrayUpload

wrapper.gradleVersion = '4.10'
